plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.epages.restdocs-api-spec' version '0.18.2'
	// ì½”í‹€ë¦° í”ŒëŸ¬ê·¸ì¸
	id 'org.jetbrains.kotlin.jvm' version '2.0.0'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.0.0'
	id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.0'
}

group = 'com.daramg'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.rest-assured:rest-assured'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// OAS
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'

	// fixture
	testImplementation("com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.21")

	// ì´ë©”ì¼ ì¸ì¦
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// íƒ€ìž„ë¦¬í”„
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	// ì½”í‹€ë¦°
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

	// jwt
	implementation 'com.auth0:java-jwt:4.4.0'

	// query dsl
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// aws s3
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1'
}

def generatedDir = 'src/main/generated'

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', generatedDir]
		}
	}
}

tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generatedDir))
}

clean {
	delete file(generatedDir)
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
	testLogging {
		events "failed"
		exceptionFormat "full"
		showStandardStreams = false

		afterTest { desc, result ->
			if (result.resultType == TestResult.ResultType.FAILURE) {
				println "\n âŒ===== FAILED: ${desc.displayName}"

				def exception = result.exception
				if (exception?.getClass()?.name == 'org.opentest4j.AssertionFailedError') {
					def expected = exception.expected.stringRepresentation
					def actual = exception.getActual().getStringRepresentation()

					println "â–¶ Reason : Assertion Failed"
					println "  - ðŸŸ¢Expected: ${expected}"
					println "  - ðŸ”´Actual  : ${actual}"
				} else if (exception?.getClass()?.name == 'org.opentest4j.BusinessException') {
					println "â–¶ Reason : ${exception.class.simpleName}"
					println "  - ${exception.errorCode.message}"
				} else if (exception != null) {
					println "â–¶ Reason : ${exception.class.simpleName}"
					println "  - ${exception.message}"
				}
				println "=======================================================================\n"
			}
		}
	}
}

openapi3{
	server = 'https://classic-daramg.duckdns.org'
	title = "Classic Daramg's API docs"
	description = 'í´ëž˜ì‹ ë“£ëŠ” ë‹¤ëžŒì¥ì˜ API ë¬¸ì„œìž…ë‹ˆë‹¤.'
	version = '0.1.0'
	format = 'yaml'
	outputDirectory = file("build/api-spec")
}

afterEvaluate {
	tasks.named('openapi3') {
		dependsOn tasks.named('test')
	}
}

tasks.named('bootJar') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	dependsOn tasks.named('openapi3')

	layered {}
	from(tasks.named('openapi3').map { it.outputDirectory }) {
		into 'BOOT-INF/classes/static/docs'
		rename { String fileName -> "openapi3.yaml" }
	}
}

tasks.withType(com.epages.restdocs.apispec.gradle.OpenApi3Task).configureEach {
	doLast {
		def openApiFile = file("build/api-spec/openapi3.yaml")
		if (openApiFile.exists()) {
			def content = openApiFile.text

			def securitySchemes = """
components:
  securitySchemes:
    cookieAuth:
      type: apiKey
      in: cookie
      name: access_token
  schemas:"""

			if (content.contains("components:\n  schemas:")) {
				content = content.replace("components:\n  schemas:", securitySchemes)
				content = content + "\nsecurity:\n  - cookieAuth: []"
				openApiFile.text = content
			}
		}
	}
}